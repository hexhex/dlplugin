/* -*- C++ -*- */

/**
 * @file   RacerAnswerScanner.lpp
 * @author Thomas Krennwallner
 * @date   Fri Mar 17 21:57:18 2006
 * 
 * @brief  Defines RacerFlexLexer::yylex() method.
 * 
 */

%{
#include <iostream>
#include <sstream>
#include <string>

// for yy::location 
#include "RacerAnswerParser.hpp"
#include "RacerAnswerDriver.h"

using namespace std;

// keep the RacerFlexLexer as close as possible to the C version
#define yylval (*(YYSTYPE*)lexval)
#define yylloc ((yy::location*)lexloc)
#define driver (*lexdrv)
%}

%option c++ noyywrap nounput batch debug
%option yyclass="RacerFlexLexer"

NEWLINE         \r?\n
BLANK           [ \t]
NUMBER          (\-)?[0-9]+
STRING          [A-Za-z][A-Za-z0-9_\-\./]*
QUOTED_STRING   \\\"[^\"]*\\\"

%x racererror raceranswer eatanswerfudge

%{
// advance the end location after each tokenizing step
#define YY_USER_ACTION  yylloc->columns(yyleng);
%}

%%

%{
// set begin location to the end location
yylloc->step();
%}


^":error"  BEGIN(racererror); return ERROR;
^":answer" BEGIN(eatanswerfudge); return ANSWER;


<eatanswerfudge>{

  [^\"]*   /* eat non-" chars */ yylloc->step();
  "\""     BEGIN(raceranswer); yylloc->step();

}


<racererror>[^\n]+ { /* get the error message until next \n */
    BEGIN(INITIAL);
    yylval.sval = new std::string(yytext);
    return STRING;
  }


<raceranswer>{

  "NIL\""       BEGIN(INITIAL); return NIL;
  "T\""         BEGIN(INITIAL); return TRUE;
  "\""          BEGIN(INITIAL); yylloc->step();

  [()|#$*?:/]   return *yytext;

  {STRING} {
    yylval.sval = new std::string(yytext);
    return STRING;
  }

  {QUOTED_STRING} {
    std::string tmp(yytext);
    yylval.sval = new std::string(tmp.substr(2, tmp.length() - 4));
    return STRING;
  }

  {NUMBER} {
    std::istringstream s(yytext);
    s >> yylval.ival;
    return NUMBER; 
  }

}

<*>{BLANK}+       /* skip blanks */ yylloc->step();

\"[^\"]*\"        /* ignore trailing ".*" */ yylloc->step();
{NEWLINE}         yylloc->lines(yyleng); return '\n';

.                 driver.error(*yylloc, "invalid character");

%%

